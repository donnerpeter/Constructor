package cons2

/**
 * @author peter
 */
class Chart {
  private LinkedHashSet<Frame> frames = []
  private LinkedList<Frame> active = []
  private List<Frame> dirty = []
  private Set<Frame> finished = []
  private Set<Frame> weak = []

  String presentable() {
    LinkedList<Frame> stack = []
    StringBuilder result = []
    for (f in frames()) {
      stack.addFirst f
      result << f.presentable(stack)
      result << "\n"
    }
    result.toString()
  }

  List<Frame> frames(boolean includeWeak = false) {
    (frames - weak) as List<Frame>
  }

  List<Frame> usages(Frame f, boolean includeWeak = false, def pattern) {
    frames(includeWeak).findAll { f in it.children && it.ping(false, pattern) }
  }

  Frame construction(String name, int theta = -1, Frame... children) {
    registerFrame(new Frame(this, name, theta, children))
  }

  List<Frame> findActive(boolean thematic, def pattern) {
    return active.findAll { it.ping(thematic, pattern) }
  }

  Frame newFrame(String name) {
    registerFrame(new Frame(this, name, -1))
  }

  private Frame registerFrame(Frame frame) {
    frames << frame
    activate(frame)
    frame
  }

  void activate(Frame frame) {
    strengthen(frame)
    active.addFirst frame
    dirty += active
  }

  private void strengthen(Frame frame) {
    weak.remove frame
    frame.children.each { strengthen it }
  }

  private Frame nextUnprocessed() {
    if (dirty) {
      return dirty.remove(0)
    }
    null
  }

  void update() {
    while (true) {
      def f = nextUnprocessed()
      if (!f) {
        return
      }
      f.activate()
    }
  }

  void wrapUp() {
    wrapUp(active as Frame[])
  }

  void wrapUp(Frame... frames) {
    Set<Frame> processed = []
    while (true) {
      def f = frames.find { !(it in processed) && !(it in finished) }
      if (!f) {
        break
      }

      if (f.wrapUp()) {
        finished << f
      }
      processed << f
    }

    active.removeAll finished
    dirty.removeAll finished
  }

  void weaken(Frame f) {
    weak << f
    active.remove f
    dirty.remove f
    f.strongUsages([]).each { weaken it }
  }
}
