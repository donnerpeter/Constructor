package cons2

/**
 * @author peter
 */
class Chart {
  List<Frame> frames = []
  private Map<Frame, IntRange> positions = [:]
  private List<Frame> dirty = []
  private Set<Frame> finished = []

  String presentable() {
    LinkedList<Frame> stack = []
    StringBuilder result = []
    for (f in frames) {
      stack.addFirst f
      result << f.presentable(stack)
      result << "\n"
    }
    result.toString()
  }

  void removeFrame(Frame frame) {
    frames -= frame
    positions.remove(frame)
    dirty -= frame
    finished -= frame
  }

  Frame newFrame(Frame arg1, Frame argN) {
    int start = Math.min(startOffset(arg1), startOffset(argN))
    int end = Math.max(endOffset(argN), endOffset(argN))
    newFrame(start..end)
  }

  List<Frame> findActive(boolean thematic, boolean eager, pattern) {
    return frames.findAll { it.respondsTo(thematic, eager, pattern) }
  }

  int endOffset(Frame f) {
    return positions[f].toInt
  }

  int startOffset(Frame f) {
    return positions[f].fromInt
  }

  Frame newFrame(Range<Integer> position) {
    def frame = new Frame(this)
    frames << frame
    positions[frame] = position
    dirty += frames
    frame
  }

  private Frame nextUnprocessed() {
    if (dirty) {
      return dirty.remove(0)
    }
    null
  }

  void update() {
    while (true) {
      def f = nextUnprocessed()
      if (!f) {
        return
      }
      f.activate()
    }
  }

  void wrapUp() {
    Set<Frame> processed = []
    while (true) {
      def f = frames.find { !(it in processed) && !(it in finished) }
      if (!f) {
        return
      }

      if (f.wrapUp()) {
        finished << f
      }
      processed << f
    }
  }
}
