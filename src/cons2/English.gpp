package cons2

 /**
 * @author peter
 */
class English implements Language {
  Inflection infl = new Inflection().
          wholeWord("father", ["syn", "noun"], ["sem", "father"]).
          wholeWord("mother", ["syn", "noun"], ["sem", "mother"]).
          wholeWord("beautiful", ["syn", "adj"], ["sem", "beautiful"]).
          wholeWord("daughter", ["syn", "noun"], ["sem", "daughter"]).
          wholeWord("the", ['syn', 'article']).
          suffix("s", ['syn', 'verb'])

  boolean wrapUp(Frame host, Aspect aspect, Update update) {
    true
  }

  SimpleQuery subject = [[syn:"noun"], ["syn", 'subject'], ["sem", "experiencer"]]
  SimpleQuery object = [[syn:"noun"], ["syn", 'object'], ["sem", "theme"]]
  SimpleQuery property_host = [[syn:"noun"], [new Aspect("sem", 'property_host')] as Aspect[]]

  void aspectAppeared(Frame host, Aspect aspect, Update update) {
    if (aspect.ping('article')) {
      def article = update.newFrame([], new AnticipatingConstruct(["noun"], [syn:"noun"]))
      update.newFrame([article, host], ["syn", "det_noun"])
      return
    }

    if (aspect.ping('verb')) {
      subject.activate(host, true, update)
      return
    }

    if (aspect.ping('adj')) {
      property_host.activate host, update
      return
    }

    if (aspect.role == 'stem') {
      if (aspect.ping('love')) {
        update.enrich(host, ["sem", "love"])
        return
      }
    }

    if (aspect.role == 'phon') {
      infl.analyze host, aspect, update
    }

  }

  void contextChanged(Frame host, Aspect aspect, Update update) {
    if (aspect.ping('verb')) {
      object.activate(host, false, update)
    }
  }

  void generate(Chart sem, Chart to) {
    sem.frames().findAll { it.ping(sem:'love') || it.ping(sem:'HAPPEN') }.each { generateSentence(it, to) }
  }

  private void generateSentence(Frame verb, Chart dst) {
    generateNP(dst, verb.opponent(sem:'experiencer'), true)
    dst.newFrame([], ['phon', verb.ping(sem:'love') ? 'loves' : 'happened'])
    def theme = verb.opponent(sem:'theme')
    if (verb.ping(sem:'love')) {
      generateNP(dst, theme, false)
    } else {
      generatePP(dst, 'to', theme)
    }
    if (verb.ping(time:'PAST')) {
      dst.newFrame([], ['phon', 'today'])
    }
  }

  private void generatePP(Chart dst, String prep, Frame noun) {
    dst.newFrame([], ['phon', prep])
    generateNP dst, noun, false
  }

  private void generateNP(Chart dst, Frame noun, boolean nom) {
    if (noun.ping(sem:'I')) {
      dst.newFrame([], ['phon', nom ? 'I' : 'me'])
      return;
    }

    dst.newFrame([], ['phon', (noun.ping(discourse:'new') ? 'An' : 'the')])
    noun.allOpponents(sem:'property_host').each { generateAdj(dst, it) }
    if (noun.ping(sem:'mother')) {
      dst.newFrame([], ['phon', 'mother'])
    } else if (noun.ping(sem:'daughter')) {
      dst.newFrame([], ['phon', 'daughter'])
    } else if (noun.ping(sem:'father')) {
      dst.newFrame([], ['phon', 'father'])
    } else if (noun.ping(sem:'THING')) {
      dst.newFrame([], ['phon', 'thing'])
    }
    dst.update()
  }

  private def generateAdj(Chart dst, Frame adj) {
    if (adj.ping(sem:'beautiful')) {
      dst.newFrame([], ['phon', 'beautiful'])
    }
    else if (adj.ping(sem:'AMAZING')) {
      dst.newFrame([], ['phon', 'amazing'])
    }
  }

}
