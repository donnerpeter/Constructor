package cons2

/**
 * @author peter
 */
class Frame {
  Chart chart
  private List<Dimension> allDimensions = []
  private Dimension chosen = null
  String name
  Frame[] children
  private int thematic

  def Frame(Chart chart, String name, int thematic, Frame... args) {
    this.chart = chart
    this.thematic = thematic
    this.children = args
    this.name = name
  }

  Dimension addAspect(String name, String... nicknames) {
    Set nicks = nicknames as Set
    nicks << name
    def element = new Dimension(nicks)
    allDimensions << element
    chosen = allDimensions.size() > 1 ? null : element
    element
  }

  List<Frame> strongUsages(pattern) {
    chart.usages(this, false, pattern)
  }

  List<Frame> allUsages(pattern) {
    chart.usages(this, true, pattern)
  }

  Frame findAround(boolean thematic, def pattern) {
    return findAllActiveAround(thematic, pattern).find { true }
  }

  List<Frame> findAllActiveAround(boolean thematic, def pattern) {
    return chart.findActive(thematic, pattern) - this
  }

  List<Frame> getThematicChildren() {
    thematic >= 0 ? [children[thematic]] : []
  }

  void chosenAs(def pattern) {
    for (a in dimensions()) {
      if (a.ping(pattern)) {
        chosen = a
        return
      }
    }
  }

  boolean ping(boolean thematic, def pattern) {
    if (thematic) {
      if (strongUsages([]).any { ((Frame) this) in it.thematicChildren }) {
        return false
      }
    }

    for (a in dimensions()) {
      if (a.ping(pattern)) {
        return true
      }
    }
    return false
  }

  private List<Dimension> dimensions() {
    return chosen ? [chosen] : allDimensions
  }

  @Override
  String toString() {
    allDimensions.collect { a, v -> "$a=$v" }.join(";")
  }

  def activate() {
    if (chosen) {
      chosen.activate this
    }
  }

  String presentable(LinkedList<Frame> stack) {
    name + children.collect { " ^" + stack.indexOf(it) }.join("")
  }

  boolean wrapUp() {
    def syn = chosen
    return !syn || syn.wrapUp.call(this)
  }

  void relax() {
    chosen = null
    chart.activate this
  }


}

