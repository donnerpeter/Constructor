package cons2

/**
 * @author peter
 */
class Frame {
  Chart chart
  private List<Dimension> allDimensions = []
  private Dimension chosen = null
  String role
  String name
  Frame[] children
  private int thematic
  private Team team

  def Frame(Chart chart, String role, String name, int thematic, Frame... args) {
    this.chart = chart
    this.thematic = thematic
    this.children = args
    this.name = name
    this.role = role
  }

  Frame joinTeam(Frame f) {
    assert !team
    team = f.team
    team.members << this
    this
  }

  Team getTeam() {
    if (!team) {
      team = new Team(this)
    }
    team
  }

  Dimension addAspect(String... nicknames) {
    def element = new Dimension(nicknames)
    allDimensions << element
    chosen = allDimensions.size() > 1 ? null : element
    element
  }

  List<Frame> strongUsages(pattern) {
    chart.usages(this, false, pattern)
  }

  Frame findAround(boolean thematic, def pattern) {
    return findAllActiveAround(thematic, pattern).find { true }
  }

  List<Frame> findAllActiveAround(boolean thematic, def pattern) {
    return chart.findActive(thematic, pattern) - this
  }

  List<Frame> getThematicChildren() {
    thematic >= 0 ? [children[thematic]] : []
  }

  void chosenAs(def pattern) {
    chosen = findDimension(pattern)
  }

  private Dimension findDimension(pattern) {
    List<Dimension> dimensions = chosen ? [chosen] : allDimensions
    return dimensions.find { it.ping(pattern) }
  }

  boolean ping(boolean thematic, def pattern) {
    if (thematic && strongUsages([]).any { ((Frame) this) in it.thematicChildren }) {
      return false
    }

    return findDimension(pattern) != null
  }

  @Override
  String toString() {
    name
  }

  def activate() {
    if (chosen) {
      chosen.activate this
    }
  }

  String presentable(LinkedList<Frame> stack) {
    name + children.collect { " ^" + stack.indexOf(it) }.join("")
  }

  boolean wrapUp() {
    return !chosen || chosen.wrapUp.call(this)
  }

  void relax() {
    chosen = null
    chart.activate this
  }


}

