package cons2

/**
 * @author peter
 */
class Frame {
  Chart chart
  private Map<String, List<Dimension>> allDims = [:]
  private Map<String, Dimension> chosenDims = [:]
  String name
  Frame[] children
  private int thematic

  def Frame(Chart chart, String name, int thematic, Frame... args) {
    this.chart = chart
    this.thematic = thematic
    this.children = args
    this.name = name
  }

  Dimension addAspect(String aspect, String name, String... nicknames) {
    def element = new Dimension(name, nicknames)
    allDims.get(aspect, []) << element
    chosenDims[aspect] = allDims[aspect].size() > 1 ? null : element
    element
  }

  List<Frame> strongUsages(pattern) {
    chart.usages(this, false, pattern)
  }

  List<Frame> allUsages(pattern) {
    chart.usages(this, true, pattern)
  }

  Frame findAround(boolean thematic, def pattern) {
    return findAllActiveAround(thematic, pattern).find { true }
  }

  List<Frame> findAllActiveAround(boolean thematic, def pattern) {
    return chart.findActive(thematic, pattern) - this
  }

  Dimension getAt(String aspectName) {
    chosenDims[aspectName]
  }

  List<Frame> getThematicChildren() {
    thematic >= 0 ? [children[thematic]] : []
  }

  void chosenAs(def pattern) {
    for (name in allDims.keySet()) {
      for (a in dimensions(name)) {
        if (a.ping(pattern)) {
          chosenDims[name] = a
        }
      }
    }
  }

  boolean ping(boolean thematic, def pattern) {
    if (thematic) {
      if (strongUsages([]).any { ((Frame) this) in it.thematicChildren }) {
        return false
      }
    }

    for (name in allDims.keySet()) {
      for (a in dimensions(name)) {
        if (a.ping(pattern)) {
          return true
        }
      }
    }
    return false
  }

  private List<Dimension> dimensions(String name) {
    return chosenDims[name] ? [chosenDims[name]] : allDims[name]
  }

  @Override
  String toString() {
    allDims.collect { a, v -> "$a=$v" }.join(";")
  }

  def activate() {
    chosenDims.values().each {
      if (it) {
        it.activate ((Frame) this)
      }
    }
  }

  String presentable(LinkedList<Frame> stack) {
    name + children.collect { " ^" + stack.indexOf(it) }.join("")
  }

  boolean wrapUp() {
    def syn = chosenDims["syn"]
    return !syn || syn.wrapUp.call(this)
  }

  void relax() {
    allDims.keySet().each {
      chosenDims[it] = null
    }
    chart.activate this
  }


}

