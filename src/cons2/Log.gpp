package cons2

 /**
 * @author peter
 */
class Log {
  final List<Event> events = []

  public void event(Event event) {
    events << event
  }

  private Set<Frame> interestingFrames(Set<String> roles) {
    events.filter { it instanceof AspectEvent && ((AspectEvent)it).role in roles }.collect { it.frame.resolve() } as Set
  }

  List<Event> interestingEvents(Set<String> roles) {
    def frames = interestingFrames(roles)
    events.findAll { it.frame.resolve() in frames && (!(it instanceof AspectEvent) || ((AspectEvent)it).role in roles) }
  }

  private List<List<Event>> groupEvents(Set<String> roles) {
    def result = []

    Frame current = null
    List<Event> group = null
    for (e in interestingEvents(roles)) {
      if (e.frame != current) {
        if (group) {
          result << group
        }
        current = e.frame
        group = []
      }
      group << e
    }
    if (group) {
      result << group
    }
    result
  }

  String presentable(Set<String> roles) {
    String out = ""
    LinkedList<Frame> stack = []
    def groups = groupEvents(roles)
    for (List<Event> group in groups) {
      if (!group) {
        continue
      }

      def frame = group[0].frame
      String line
      if (stack.indexOf(frame) >= 0) {
        line = frameReference(stack, frame)
      } else {
        assert group[0] instanceof FrameEvent
        line = "frame" + frame.args.collect { " " + frameReference(stack, it) }.join("")
        group.remove(0)
      }
      if (group) {
        line += ": " + group.collect { it.presentable(stack) }.join(", ")
      }
      stack.addFirst frame
      out += line + "\n"
    }
    out
  }

  private static String frameReference(LinkedList<Frame> stack, Frame frame) {
    def idx = stack.indexOf(frame)
    if (idx < 0) {
      return "^_"
    }

    return "^" + (idx + 1)
  }

  static abstract class Event {
    final Frame frame

    Event(Frame frame) {
      this.frame = frame
    }

    abstract String presentable(LinkedList<Frame> stack)
  }

  static class AspectEvent extends Event {
    final String role
    final Aspect newValue

    def AspectEvent(Frame frame, String role, Aspect newValue) {
      super(frame)
      this.role = role;
      this.newValue = newValue;
    }

    @Override
    String toString() {
      "$frame: $role=$newValue"
    }

    @Override
    String presentable(LinkedList<Frame> stack) {
      "$role=$newValue"
    }

  }

  static class FrameEvent extends Event {
    final boolean weakened

    FrameEvent(Frame frame, boolean weakened) {
      super(frame)
      this.weakened = weakened
    }

    @Override
    String toString() {
      (weakened ? "-" : "+") + frame
    }

    @Override
    String presentable(LinkedList<Frame> stack) {
      "weaken"
    }

  }

  static class MergeEvent extends Event {
    final Frame loser

    MergeEvent(Frame frame, Frame loser) {
      super(frame)
      this.loser = loser
    }

    @Override
    String presentable(LinkedList<Frame> stack) {
      "==" + Log.frameReference(stack, loser)
    }


  }
}
