package cons2

 /**
 * @author peter
 */
class Russian implements Language {
  Inflection infl = new Inflection().
          wholeWord("мать", ["syn", "noun", "nom"], ["syn", "noun", "acc"], ["sem", "mother"]).
          wholeWord("дочь", ["syn", "noun", "nom"], ["syn", "noun", "acc"], ["sem", "daughter"]).
          wholeWord("отец", ["syn", "noun", "nom"], ["sem", "father"]).
          wholeWord("отца", ["syn", "noun", "acc"], ["sem", "father"]).
          suffix("ит", new SynVerb()).
          suffix("ая", ['syn', 'adj', 'adj_nom']).
          suffix("ую", ['syn', 'adj', 'adj_acc'])


  void analyzeWord(Chart chart, String w) {
    chart.newFrame(['phon', w])
  }

  void aspectAppeared(Frame host, Construct aspect) {
    if (aspect.role == 'phon') {
      infl.analyze host, aspect
      return
    }

    if (aspect.ping('adj')) {
      def caze = aspect.ping('adj_nom') ? 'nom' : 'acc'
      def adj = new SimpleQuery(caze, false, ['syn', 'adjective_noun'], ['sem', 'property_host'])
      if (!adj.activate(host)) {
        host.chart.newFrame(new AnticipatingConstruct(["noun", caze], "noun"))
      }
      return
    }

    aspect.appeared host
    
    if (aspect.role == 'stem') {
      def stem = aspect.nicknames.iterator().next()
      if (stem == "люб") {
        host.enrich(["sem", "love"])
      }
      if (stem == "красив") {
        host.enrich(["sem", "beautiful"])
      }
    }

  }

  void contextChanged(Frame host, Construct aspect) {
    if (aspect.ping('adj')) {
      def caze = aspect.ping('adj_nom') ? 'nom' : 'acc'
      new SimpleQuery(caze, false, ['syn', 'adjective_noun'], ['sem', 'property_host']).activate(host)
      return
    }


    aspect.contextChanged host
  }

  boolean wrapUp(Frame host, Construct aspect) {
    aspect.wrapUp(host)
  }

  SimpleQuery subject = ["nom", true, ["syn", 'subject'], ["sem", "state_experiencer"]]
  SimpleQuery object = ["acc", true, ["syn", 'object'], ["sem", "state_theme"]]

  class SynVerb extends Construct {

    def SynVerb() {
      super("syn", "verb")
    }

    @Override
    void contextChanged(Frame f) {
      subject.activate f
      object.activate f
    }

    @Override
    void appeared(Frame host) {
      subject.activate host, true
      object.activate host, true
    }

    @Override
    boolean wrapUp(Frame f) {
      if (subject.satisfied(f) && object.satisfied(f)) {
        return true
      }

      subject.weaken(f)
      object.weaken(f)

      def subjs = subject.allArgumentCandidates(f)
      def objs = object.allArgumentCandidates(f)

      if (objs.size() == 1) {
        subjs -= objs
        object.instantiate(f, objs[0])
      }
      if (subjs.size() == 1) {
        subject.instantiate(f, subjs[0])
      }

      return subject.satisfied(f) && object.satisfied(f)
    }


  }

}
