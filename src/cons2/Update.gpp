package cons2

/**
 * @author peter
 */
class Update {
  Chart chart
  private Map<Frame, Set<Aspect>> newAspects = [:]
  private Map<Frame, Set<Aspect>> justAppeared = [:]
  private Map<Frame, Map<String, Object>> confirmations = [:]
  private Set<Frame> wrapUps = []
  private Set<Frame> weakened = []
  private Frame beingProcessed
  private Map<Frame, Map<String, List<Function2<Frame, Update, Boolean>>>> newListeners = [:]
  private boolean dead

  Update(Chart chart, Map<Frame, Set<Aspect>> justAppeared = [:], Set<Frame> wrapUps = []) {
    this.chart = chart
    this.justAppeared = justAppeared
    this.wrapUps.addAll wrapUps
  }

  void enrich(Frame host, Aspect... aspects) {
    assert !dead
    assert host
    newAspects.get(host, [] as LinkedHashSet).addAll(aspects)
  }

  Frame newFrame(List<Frame> children, Aspect... aspects) {
    assert !dead
    def frame = chart.newFrame(children)
    enrich frame, aspects
    frame
  }

  void wrapUp(Frame frame) {
    assert !dead
    wrapUps << frame
  }

  void confirmChoice(Map<String, Object> pattern, Frame frame) {
    assert !dead
    confirmations.get(frame, [:]).putAll pattern
  }

  void weaken(Frame frame) {
    assert !dead
    weakened << frame
  }

  void listen(String role, Function2<Frame, Update, Boolean> listener) {
    assert !dead
    assert beingProcessed != null
    def map = newListeners.get(beingProcessed, [:])
    map.get(role, []) << listener
  }

  void apply() {
    weakened.each { chart.weaken it }
    confirmations.each { frame, pattern -> frame.chosenAs(pattern) }

    chart.listeners.putAll newListeners

    if (!newAspects && !justAppeared) {
      wrapUps.each { chart.wrapUp it }
      return
    }

    def next = new Update(chart, newAspects, wrapUps)

    justAppeared.each { frame, added ->
      added.each {
        if (!frame[it.role] || frame[it.role] == it) {
          next.beingProcessed = frame
          chart.language.aspectAppeared frame, it, next
          next.beingProcessed = null
        }                          
      }
    }

    Map<String, Set<Frame>> touched = [:]
    newAspects.each { frame, aspects ->
      aspects.each { touched.get(it.role, []) << frame }

      frame.enrich(aspects as Aspect[])
    }

    chart.bindAnticipators next

    chart.listeners.values().each { map ->
      map.each { role, actions ->
        if (role in touched.keySet()) {
          touched[role].each { def host ->
            List<Function2<Frame, Update, Boolean>> copy = [] + actions
            copy.each { action ->
              next.beingProcessed = host
              def satisfied = action.call(host, next)
              next.beingProcessed = null
              if (satisfied) {
                actions.remove action
              }
            }
          }
        }
      }
    }

    justAppeared.each { frame, added -> frame.initializeAspects added }

    dead = true

    next.apply()
  }

}