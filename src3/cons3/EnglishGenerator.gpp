package cons3

/**
 * @author peter
 */
class EnglishGenerator {
  List<String> output = []
  Set<Situation> handled = []
  boolean past

  String generate(Chart chart) {
    chart.situations.each {
      if (!(it in handled)) {
        new SituationGenerator(chart, it, true).sentence()
      }
    }

    String result = ""
    for (w in output) {
      if (!result.empty && !(w in ['.', ','])) {
        result += " "
      }
      result += w
    }
    return result
  }

  private class SituationGenerator {
    final Chart chart
    final Situation situation
    final Frame sitFrame
    boolean hasSubject
    boolean capitalize

    SituationGenerator(Chart chart, Situation situation, boolean capitalize) {
      this.chart = chart
      this.situation = situation
      this.capitalize = capitalize
      sitFrame = situation.frame(chart)
    }
    
    private void out(String word) {
      output << (capitalize ? word.capitalize() : word)
      capitalize = false
    }

    private def sentence() {
      handled << situation

      if (sitFrame.s('time') == 'PAST') {
        past = true //todo maintain a tense for the whole discourse
      }


      def excl = sitFrame.f('exclamation')
      if (excl) {
        out 'great'
        out 'was'
        def a = chart.allAssignments(situation).find { it.frame != sitFrame && it.value == excl }
        if (a) {
          np(a.frame, true)
        }
        return
      }

      List<Frame> allVerbs = chart.frames(situation).findAll { it.type in ['HAPPEN', 'FORGET', 'COME_SCALARLY', 'GO_OFF', 'ASK', 'DISCOVER', 'ALSO', 'REMEMBER'] }
      def verb = allVerbs[0]
      assert verb : situation.presentable(chart)

      clause(verb)

      if (allVerbs.size() > 1) {
        out "and"
        clause(allVerbs[1])
      }

      if (sitFrame.f('but')) {
        out ','
        out 'but'
        def generator = new SituationGenerator(chart, (Situation) sitFrame.f('but').var, false)
        generator.hasSubject = true
        generator.sentence()
      }

      if (sitFrame.s('dot') == 'true') {
        out '.'
      }
    }

    private void clause(Frame verb) {
      def subj = verb.f('arg1')
      if (!hasSubject) {
        assert subj : "$verb; " + situation.presentable(chart)
        np(subj, true)
        hasSubject = true
      }

      if (verb.s('manner') == 'SUDDENLY') {
        out 'suddenly'
      }
      def type = verb.type
      if (type != 'ALSO') {
        def verbs = [HAPPEN: 'happened', FORGET: 'forgot', COME_SCALARLY: 'comes first', GO_OFF: 'went', ASK: 'asked', DISCOVER: 'discovered', CAN: "couldn't", RECALL:'recall', REMEMBER:(past ? 'remembered' : 'remember')]
        out(verbs[type])
      }

      def dobj = verb.f('arg2')
      if (dobj) {
        np(dobj, false)
      }

      def io = verb.type == 'HAPPEN' ? verb.f('experiencer') : verb.type == 'GO_OFF' ? verb.f('goal') : null
      if (io) {
        out "to"
        np(io, false)
      }

      if (verb.type == 'HAPPEN') {
        out "today"
      }

      def question = verb.f('question')
      if (question?.var instanceof Situation) {
        def questSituation = (Situation) question.var
        handled << questSituation
        out 'about'
        def think = chart.frames(questSituation).find { it.type == 'THINK' }
        def exp = think.f('arg1')
        if (exp?.type == 'THEY') {
          out 'their'
        }
        out 'opinion'
        out 'on'
        np(think.f('topic'), false)
      }

      def theme = verb.f('theme')
      if (theme?.var instanceof Situation) {
        if (!theme.f('questioned')) {
          out ","
          out "that"
        }

        new SituationGenerator(chart, (Situation)theme.var, false).sentence()
      } else if (theme && verb.type in ['CAN', 'ALSO']) {
        clause(theme)
      }

      def elaboration = sitFrame.f('elaboration')
      if (elaboration?.var instanceof Situation) {
        out ","
        new SituationGenerator(chart, (Situation)elaboration.var, false).sentence()
      }


      def variants = subj?.f('variants')
      if (variants) {
        out '-'
        npseq(variants)
      }
    }

    def npseq(Frame seq) {
      def members = seq.allAssignments('member')
      members.eachWithIndex { Assignment<Frame> var, int index ->
        if (index == members.size() - 1) out seq.s('conj')
        else if (index > 0) out ','
        np((Frame) var.value, true)
      }
    }

    private def np(Frame n, boolean nom) {
      if (!n) {
        out '???'
        return
      }

      if (n.s('type') == 'ME') {
        out (nom ? 'I' : "me")
        return
      }
      if (n.s('type') == 'THEY') {
        out (nom ? 'they' : "them")
        return
      }

      if (n == sitFrame.f('questioned')) { //todo a type for seq frames
        out 'what'
        return
      }

      if (n.type == 'NEXT') {
        out 'what comes next' //todo a non-hacky model for 'what comes next'
        return
      }

      if (n.s('number') == 'true') {
        out n.type
        return
      }

      if (n.f('member')) {
        npseq(n)
        return
      }

      determiner(n)
      if (n.s('property') == 'AMAZING') {
        out "amazing"
      }
      def criterion = n.f('criterion')
      if (criterion) {
        out noun(criterion.type)
      }

      out noun(n.type)

      if (n.type == 'AMAZE') {
        def theme = n.f('theme')
        if (theme?.var instanceof Situation) {
          out ','
          out 'when'
          new SituationGenerator(chart, (Situation) theme.var, false).sentence()
        }
      }
    }

    private String determiner(Frame n) {
      def det = n.s('determiner')
      if (det == 'THIS') {
        out 'this'
        return
      }

      if (n.type == 'AMAZE') {
        def assignments = n.allAssignments('arg1')
        if (!assignments.empty) {
          possessive((Frame)assignments[0].value)
          if (assignments.size() > 1) {
            out 'and'
            possessive((Frame)assignments[1].value)
          }

          return
        }

      }

      if (n.type == 'NEIGHBOURS') {
        out 'my' //todo using the actual inferred model information
        return
      }

      if (n.s('given') != 'false') {
        out 'the'
        return
      }

      out "An"
    }

    private void possessive(Frame poss) {
      if (poss.type == 'ME') out 'my'
      else if (poss.type == 'THEY') out 'their'
      else out poss.type
    }

  }

  private String noun(String type) {
    switch (type) {
      case 'THING': return 'thing'
      case 'NEIGHBOURS': return 'neighbors'
      case 'MATTER': return 'matter'
      case 'AMAZE': return 'amazement'
      case 'COUNTING': return 'counting'
      case 'ORDER': return 'order'
      default: return type
    }

  }

}
