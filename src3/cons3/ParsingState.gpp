package cons3

/**
 * @author peter
 */
class ParsingState {
  static final Closure noInit = { it }

  final Chart chart
  final Situation situation
  final Map<Construction, Map> constructions
  final FLinkedMap<Construction, Map> history

  ParsingState(Map map) {
    chart = map.chart
    situation = map.situation
    constructions = Collections.unmodifiableMap((Map)map.constructions)
    history = map.history
  }

  private ParsingState clone(Map update) {
    Map current = [chart:chart, situation:situation, constructions:constructions, history:history]
    current.putAll(update)
    return new ParsingState(current)
  }

  ParsingState clearHistory() {
    clone(history:FLinkedMap.emptyMap)
  }

  ParsingState withSituation(Situation situation) { clearConstructions().clone(situation:situation, history:FLinkedMap.emptyMap) }

  Variable newVariable() {
    new Variable()
  }

  ParsingState assign(Variable var, String property, def value) {
    clone(chart: chart.assign(var, situation, property, value))
  }

  Map getAt(Construction construction) {
    constructions[construction]
  }

  ParsingState apply(Map newArgs = [:], Construction name, Closure init = null) {
    return addCtx(newArgs, name, init).applyAll(name)
  }

  private boolean overwrites(Construction name, Map newArgs) {
    def oldArgs = constructions[name]
    if (!oldArgs) {
      return true
    }
    for (arg in oldArgs.keySet().intersect(newArgs.keySet())) {
      if (arg != 'init' && oldArgs[arg] != newArgs[arg] && oldArgs[arg]) {
        return true
      }
    }
    return false
  }

  ParsingState addCtx(Map newArgs, Construction name, Closure init = null) {
    def replace = overwrites(name, newArgs)
    Map oldArgs = replace ? [:] : constructions[name]
    def args = oldArgs + newArgs
    Closure oldInit = args['init']
    def newInit = init && oldInit ? { ParsingState state -> init(oldInit(state)) } : init ?: oldInit
    args.init = newInit
    def newConstructions = constructions + [(name): Collections.unmodifiableMap(args)]
    return (replace && constructions[name] != null ? inhibit(name) : this).clone(constructions: newConstructions)
  }

  ParsingState inhibit(Construction... cxts) {
    def state = this
    for (c in cxts) {
      state = state.removeConstruction(c)
    }

    def freed = [] as LinkedHashSet
    Set<Construction> remaining = state.constructions.keySet() as LinkedHashSet
    remaining.removeAll(cxts as List)
    remaining.each {
      for (c in cxts) {
        if (contradict(it, c) || contradict(c, it)) {
          freed << it
        }
      }
    }
    return state.applyAll(freed as Construction[])
  }

  ParsingState satisfied(Construction name) {
    return removeConstruction(name).clone(history: (history + [(name): constructions[name]]))
  }

  private ParsingState removeConstruction(Construction name) {
    def newConstructions = new HashMap(constructions)
    newConstructions.remove(name)
    return clone(constructions: newConstructions)
  }

  ParsingState apply(Map<Construction, Map<String, Object>> constructions) {
    def result = this
    Set<Construction> fresh = [] as LinkedHashSet
    Set<Construction> old = [] as LinkedHashSet
    Set<Construction> replaced = [] as LinkedHashSet
    for (cxt in constructions.keySet()) {
      if (this.constructions[cxt]) {
        if (overwrites(cxt, constructions[cxt])) {
          replaced << cxt
        } else {
          old << cxt
        }
      } else {
        fresh << cxt
      }
    }
    result = result.inhibit(replaced as Construction[])

    for (cxt in constructions.keySet()) {
      result = result.addCtx(constructions[cxt], cxt)
    }

    for (cxt1 in (fresh + replaced)) {
      for (cxt2 in old) {
        if (result.contradict(cxt1, cxt2) || result.contradict(cxt2, cxt1)) {
          result = result.inhibit(cxt1)
        }
      }

    }

    def cs = constructions.keySet() as LinkedHashSet
    cs.retainAll(result.constructions.keySet())
    return result.applyAll(cs as Construction[])
  }

  ParsingState applyAll(Construction... names) {
    Set<Construction> hanging = [] as LinkedHashSet
    names.each { name1 ->
      names.each { name2 ->
        if (contradict(name1, name2)) {
          hanging << name1
          hanging << name2
        }
      }
    }

    ParsingState result = this
    LinkedHashSet<Construction> others = (constructions.keySet() as LinkedHashSet) - (names as List)
    for (name1 in names) {
      for (name2 in others) {
        if (contradict(name1, name2) || contradict(name2, name1)) {
          result = result.inhibit(name2)
        }
      }
    }

    for (cxt in names) {
      if (!(cxt in hanging)) {
        if (result[cxt]) {
          result = result.doApply(cxt)
        }
      }
    }

    for (cxt1 in hanging) {
      for (cxt2 in hanging) {
        if (cxt1 != cxt2 && result[cxt1] && result[cxt2]) {
          def alt1 = result.apply(cxt1)
          def alt2 = result.apply(cxt2)
          if (morePlausible(alt1, alt2)) {
            result = result.inhibit(cxt2)
          }
          else if (morePlausible(alt2, alt1)) {
            result = result.inhibit(cxt1)
          }
        }
      }
    }

    return result
  }

  boolean isHuman(Frame f) {
    return !f.s('number')
  }

  boolean morePlausible(ParsingState st1, ParsingState st2) {
    def remember2 = st2.chart.frames(st2.situation).find { it.type in ['REMEMBER', 'FORGET'] }
    def remember1 = st1.chart.frames(st1.situation).find { it.type in ['REMEMBER', 'FORGET'] }
    if (remember2 && remember1 && !remember2.conjArg('arg1').every { isHuman(it) } && remember1.conjArg('arg1').every { isHuman(it) }) {
      return true
    }

    if (st2.constructions.keySet().find { it.name == 'nom' } && st2.chart.frames(st2.situation).find { it.type in ['COME_SCALARLY', 'FORGET'] }?.f('arg1')) {
      return true //todo prohibit two nom's in one situation
    }

    return false
  }

  ParsingState doApply(Construction cxt) {
    def args = constructions[cxt]
    Closure init = args.init ?: noInit
    return cxt.action.call((ParsingState)init(this), args)
  }


  ParsingState clearConstructions() {
    return clone(constructions: [:])
  }

  ParsingState restore(Map saved) {
    clone(constructions: saved + this.constructions)
  }

  boolean contradict(Construction name1, Construction name2) {
    if (name1.name in ['nom', 'prep', 'vPrep'] && name2.name in ['acc', 'vAcc'] && this[name1].noun && this[name2].noun) {
      if (this[name1].noun == this[name2].noun) return true

      Map base1 = this[name1].base
      Map base2 = this[name2].base
      if (base1?.noun) {
        return base1.noun in [this[name2].noun, base2?.noun]
      }
      if (base2?.noun) {
        return base2.noun in [this[name1].noun, base1?.noun]
      }
      return false
    }
    if (name1.name == 'summerGarden' && name2.name == 'adjective') {
      return true
    }
    if (name1.name == 'comeScalarly' && name2.name == 'advObj') {
      return true
    }


    return false
  }
}
