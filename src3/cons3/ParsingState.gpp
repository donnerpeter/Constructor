package cons3

/**
 * @author peter
 */
class ParsingState {
  static final ParsingState EMPTY = new ParsingState()
  final Chart chart
  private final FLinkedMap<Mite, Integer> miteGenerations
  final List<Mite> contribution
  final Network network
  final int currentGeneration
  private Map<Construction, List<Mite>> vmCache
  final List<Mite> ownMites
  final ParsingState prevState

  ParsingState() {
    this(chart: new Chart(),
            ownMites: [],
            prevState:EMPTY,
            contribution:[],
            network: new Network([:], [:], []), currentGeneration:0, miteGenerations:FLinkedMap.emptyMap, byXor:Collections.emptyMap())
  }

  ParsingState(Map map) {
    chart = map.chart
    currentGeneration = map.currentGeneration
    assert (map.active instanceof LinkedHashSet) || !(map.active instanceof HashSet)
    assert (map.groups instanceof LinkedHashSet) || !(map.groups instanceof HashSet)
    network = map.network
    miteGenerations = map.miteGenerations
    contribution = map.contribution
    ownMites = map.ownMites
    prevState = map.prevState
  }

  List<Mite> getMiteList() {
    return visibleMites.values().collect { it.reverse() }.flatten() as List
  }

  Map<Construction, List<Mite>> getVisibleMites() {
    if (vmCache == null) {
      vmCache = this.calcVisibleMites()
    }
    return vmCache
  }

  private ParsingState clone(Map update) {
    Map current = [chart:chart, ownMites:ownMites, prevState:prevState, network:network,
            currentGeneration:currentGeneration, miteGenerations:miteGenerations, contribution:contribution]
    current.putAll(update)
    return current == update ? this : new ParsingState(current)
  }


  ParsingState assign(def var, String property, def value) {
    clone(chart: chart.assign(((Variable)var).base, property, value instanceof Variable ? value.base : value, currentGeneration))
  }

  ParsingState advanceSection() {
    clone(chart: chart.advanceSection())
  }

  Map getAt(Construction c) {
    def allMites = visibleMites[c]
    if (allMites == null) {
      return null
    }

    for (Mite mite in allMites.reverse()) {
      if (network.isChosen(mite)) {
        return mite.contents
      }
    }

    Map value = [:]
    for (part in allMites.reverse()) {
      if (!Mite.overwrites(value, part.contents)) {
        value = Mite.unify(value, part.contents)
      }
    }
    value
  }

  @Override
  String toString() {
    return "#$currentGeneration"
  }

  ParsingState startMeta(Variable meta) {
    return clone(chart: chart.startMeta(meta))
  }
  ParsingState finishMeta(Variable meta) {
    return clone(chart: chart.finishMeta(meta))
  }

  private Mite nextInterceptor(Construction start) {
    def mites = visibleMites
    List<Construction> cs = mites.keySet() as List
    def index = cs.indexOf(start)
    if (index >= 0 || !start) {
      def tail = cs.subList(index + 1, cs.size())
      for (cxt in tail) {
        def mite = mites[cxt].reverse().find { it.contents.interceptor }
        if (mite) {
          return mite
        }
      }
    }
    return null
  }

  ParsingState apply(Map<Construction, Map> constructions) {
    apply((constructions.keySet() as List<Construction>).collect { it(constructions[it]) } as List)
  }
  ParsingState apply(Mite... constructions) { apply(constructions as List) }
  ParsingState apply(List<Mite> constructions) {
    if (!constructions) return this

    def interceptor = nextInterceptor(null)
    if (interceptor) {
      return ((Interceptor) interceptor.contents.interceptor).intercept(constructions, this, { c, s -> baseApply(s, c, true) })
    }
    return baseApply(this, constructions, true)
  }

  private static ParsingState baseApply(ParsingState state, List<Mite> update, boolean mayCorrectRoute) {
    LinkedHashSet<Mite> fullUpdate = update as LinkedHashSet<Mite>
    fullUpdate.addAll(state.enrichUpdate(update))
    assert fullUpdate

    def newMiteGenerations = state.miteGenerations
    for (mite in fullUpdate) {
      newMiteGenerations = newMiteGenerations.putValue(mite, state.currentGeneration)
    }
    state = state.clone(ownMites:fullUpdate as List, prevState:state,
            miteGenerations:newMiteGenerations, network:state.network.addMites(fullUpdate),
            contribution:update, currentGeneration:state.currentGeneration + 1)

    def newActive = state.network.choose(fullUpdate)
    state = state.activate(newActive)

    for (cxt in state.visibleMites.keySet()) {
      if (!(cxt in state.visibleMites)) continue

      List<Mite> toCheck = (fullUpdate as List<Mite>).findAll { it.cxt == cxt }
      def last = state.visibleMites[cxt].reverse()[0]
      if (last) {
        toCheck << last
      }

      for (toAdd in toCheck.reverse()) {
        if (!state.network.isChosen(toAdd)) {
          state = tryAlternative(state, toAdd)
        }
      }
    }

    if (mayCorrectRoute && state.findTraitor()) {
      state = correctRoute(state.prevState, FList.fromList([state]))
    }

    return state
  }

  private static ParsingState correctRoute(ParsingState state, FList<ParsingState> reapply) {
    for (mite in state.miteList) {
      if (mite in state.miteList && !state.network.isChosen(mite)) {
        def anotherActive = state.network.choose([mite])
        def alternative = state.activate(anotherActive)
        for (step in reapply) {
          alternative = baseApply(alternative, step.contribution, false)
        }
        if (!alternative.findTraitor()) {
          return alternative
        }
      }
    }
    if (state.prevState) {
      return correctRoute(state.prevState, reapply.prepend(state))
    }
    return state
  }

  private Mite findTraitor() {
    miteList.find { it.cxt.wrongRoute(it, this) }
  }

  ParsingState getPrevVisibleState() {
    getPrevStateFromLink(findLink(null), null)
  }

  private Mite findLink(Mite via) {
    doFindLink(via, true) ?: doFindLink(via, false)
  }
  private Mite doFindLink(Mite via, boolean up) {
    ParsingState bestPrev = null
    Mite nextVia = null
    for (mite in ownMites) {
      if (!network.isChosen(mite)) continue

      def somePrev = mite.cxt.showPrev(mite, prevState, via, up)
      if (somePrev == EMPTY) {
        return mite
      }
      if (somePrev && (bestPrev == null || bestPrev.isMoreRecentThan(somePrev))) {
        bestPrev = somePrev
        nextVia = mite
      }
    }
    return nextVia
  }

  private LinkedHashSet<Mite> findAllLinks(Mite via) {
    def link = findLink(via)
    if (!link) return []

    def prev = getPrevStateFromLink(link, via)
    assert prev
    return ownMites.findAll { prev == getPrevStateFromLink(it, via, null) } as LinkedHashSet
  }

  boolean isMoreRecentThan(ParsingState state) {
    return state == this || prevState?.isMoreRecentThan(state)
  }

  LinkedHashSet<Mite> enrichUpdate(List<Mite> update) { _enrichUpdate(update, this) }

  private static LinkedHashSet<Mite> _enrichUpdate(List<Mite> update, ParsingState state) {
    ParsingState original = state
    LinkedHashSet<Mite> fullUpdate = []
    Mite via = null
    while (state) {
      Mite link = state.findLink(via)
      Set<Mite> links = state.findAllLinks(via)
      for (mite in state.ownMites) {
        if (!links || links.find { !original.network.contradict(mite, it) }) {
          fullUpdate.addAll((mite.cxt.enrichAction(mite, update, state) as List<Mite>).reverse())
        }
      }
      state = state.getPrevStateFromLink(link, via)
      if (link) {
        update = update.findAll { link.cxt.filterUpdate(link, it) }
      }
      via = link
    }
    fullUpdate
  }

  ParsingState getPrevStateFromLink(Mite link, Mite via, ParsingState defValue = prevState) {
    link?.cxt?.showPrev(link, prevState, via, true) ?: link?.cxt?.showPrev(link, prevState, via, false) ?: defValue
  }

  private static ParsingState tryAlternative(ParsingState state, Mite toAdd) {
    def anotherActive = state.network.choose([toAdd])
    def alternative = state.activate(anotherActive)
    try {
      if (alternative.dominates(state)) {
        return alternative
      }
    } catch (Throwable e) {
      println alternative.chart.presentable() + "\n\n"
      println alternative.presentable() + "\n\n"
      throw e
    }
    return state
  }

  private static ParsingState activate(ParsingState state, Network newActive) {
    def oldActive = state.network
    state = state.clone(network:newActive)

    def toRemove = Util.minus(oldActive._chosen, newActive._chosen)

    LinkedHashSet<Mite> toApply = Util.minus(newActive._chosen, oldActive._chosen)
    toApply = Util.intersect(toApply, state.ownMites) + toApply + Util.intersect(state.miteList, newActive._chosen)

    state = state.undoChanges(toRemove)
    state = state.clone(chart:addNewUnifications(state, toApply))

    boolean changed = true
    while (changed) {
      changed = false
      for (mite in toApply) {
        if (state.network.isChosen(mite) && mite.contents && mite.cxt.isExecutable([mite])) {
          def prev = state
          state = prev.doApply(mite)
          if (prev.chart != state.chart) {
            changed = true
            break
          }
        }
      }
    }
    state
  }

  private static Chart addNewUnifications(ParsingState state, LinkedHashSet<Mite> toApply) {
    def ch = state.chart
    for (mite in toApply) {
      ch = ch.startMeta(mite)
      for (pair in mite.unifications) {
        ch = ch.addUnificationConstraint(pair.first.base, pair.second.base)
      }
      ch = ch.finishMeta(mite)
    }
    return ch
  }

  private ParsingState undoChanges(Collection<Mite> toRemove) {
    ParsingState state = this
    for (mite in toRemove) {
      state = state.clone(chart:state.chart.removeDifference(mite))
    }
    return state
  }

  private boolean dominates(ParsingState other) {
    if (network.unsatisfiedDaughters.size() < other.network.unsatisfiedDaughters.size() &&
        !other.dominatesSemantically(this)) {
      return true
    }

    return dominatesSemantically(other)
  }

  private boolean dominatesSemantically(ParsingState other) {
    def humanActions = ['GO', 'BREAK', 'CAN', 'REMEMBER', 'FORGET']
    def verbs = humanActions + ['COME_SCALARLY', 'fact', 'question']
    for (frame in chart.frames) {
      def alternative = frame.unifiedVar.frame(other.chart)
      if (frame.f('member') && alternative.flatten().every { it.type != null } && !frame.flatten().every { it.type != null }) {
        return false
      }
      if (!frame.type && alternative.type && frame.f('quantifier')?.type) {
        return false
      }
    }
    for (verb in chart.frames.findAll { it.type in verbs }) {
      def alternative = verb.unifiedVar.frame(other.chart)

      if (verb.type in humanActions ||
          verb.type == 'COME_SCALARLY' && verb.f('anchor')?.canBeHuman() && alternative.type == 'COME_SCALARLY' && alternative.f('anchor')?.canBeHuman()) {
        boolean meHuman = !verb.f('arg1')?.definitelyNonHuman()
        def otherHuman = !alternative.f('arg1')?.definitelyNonHuman()
        if (meHuman != otherHuman) {
          return meHuman
        }
      }

      if (verb.type == 'COME_SCALARLY' && verb.f('anchor')?.definitelyNonHuman() && verb.f('arg1')?.definitelyNonHuman() && alternative.type == 'GO') {
        return true
      }

      if (verb.type && verb.type != 'fact' && !alternative.type) {
        return true
      }

      if (verb.type == 'GO' && alternative.type == 'COME_SCALARLY' && alternative.f('anchor')?.canBeHuman()) {
        return true
      }

      if (((Frame) verb?.definedAttributeValue('arg1'))?.definedAttributeValue('type') &&
          !((Frame) alternative?.definedAttributeValue('arg1'))?.definedAttributeValue('type')) {
        return true
      }

      if (verb.type == 'FORGET' && verb.f('arg2') && !alternative.f('arg2')) {
        return true
      }

      if (verb.type == 'question' && alternative.type == 'fact') {
        def controller = alternative.findClauseController()
        if (!controller || controller.type == 'ASK') {
          return true
        }
      }

    }

    return false
  }

  private ParsingState doApply(Mite mite) {
    ParsingState next = clone(currentGeneration:miteGenerations[mite]?:currentGeneration, chart:chart.startMeta(mite))
    next = mite.cxt.action.call(next, mite.contents)
    return next.clone(currentGeneration:currentGeneration, chart:next.chart.finishMeta(mite))
  }

  String presentable() {
    def log = ""
    def mites = visibleMites
    def link = findLink(null)
    def links = findAllLinks(null)
    log += " -- $this->${getPrevStateFromLink(link, null)},  uni: " + chart.__unifications.keySet().collect { "${it.iterator().next()}=" + (it as List)[1] }.join(", ") + "\n"
    for (k in mites.keySet()) {
      log += "    $k: " + mites[k].collect {
        (it in links ? '!!' : '') + (network.isChosen(it) ? '*' : '') +
        new LinkedHashMap(it.contents.reverse()) +
        (it.unifications ? ';' + it.unifications.collect { "<$it.first=$it.second>" }.join("") : '')
      }.join(" ") +"\n"
    }
    return log
  }

  ParsingState findState(Mite mite, String attr) {
    findState(mite.primaries.find { it.contents[attr] })
  }
  ParsingState findState(Mite mite) {
    if (mite in ownMites) {
      return this
    }
    return prevState?.findState(mite)
  }

  List<ParsingState> getHistory() {
    def result = []
    def each = this
    while (each) {
      result << each
      each = each.prevState
    }
    result
  }

  private static Map<Construction, List<Mite>> calcVisibleMites(ParsingState state) {
    Map<Construction, List<Mite>> result = [:]
    ParsingState original = state
    Mite via = null
    while (state) {
      Mite link = state.findLink(via)
      state.ownMites.groupBy { it.cxt }.each { cxt, group ->
        result[cxt] = (group + (result[cxt] ?: [])) as List
      }
      state = state.getPrevStateFromLink(link, via)
      via = link
    }
    result
  }

}