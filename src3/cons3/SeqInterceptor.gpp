package cons3

import groovy.transform.TupleConstructor

/**
 * @author peter
 */
@TupleConstructor
class SeqInterceptor implements Interceptor, RussianConstructions {
  ParsingState before
  String conj

  int findSimilar(List<Contribution> history, Map<Construction, Map> constructions) {
    int i = 0
    for (old in history) {
      if (Parser.areSimilar(old.apps, constructions)) {
        return i
      }
      if (old.apps[nom]?.head && !old.apps[nom].noun && !constructions[complementizer] &&
          (constructions[dat]?.hasNoun || constructions[preposition] || constructions[gen]?.hasNoun || constructions[nom]?.hasNoun || constructions[acc]?.hasNoun)) {
        return -1
      }
      i++
    }
    return -1
  }

  static boolean agree(agr1, agr2) {
    return agrUnifiable(agrGender((String)agr1), agrGender((String)agr2)) &&
           agrUnifiable(agrNumber((String)agr1), agrNumber((String)agr2)) &&
           agrUnifiable(agrPerson((String)agr1), agrPerson((String)agr2))
  }

  static String agrGender(String agr) { return agr == 'masc' ? 'masc' : agr == 'fem' ? 'fem' : null }
  static String agrNumber(String agr) { return agr?.endsWith('pl') ? 'pl' : agr?.endsWith('sg') || agr in ['masc', 'fem'] ? 'sg' : null }
  static String agrPerson(String agr) { return agr?.startsWith('3') ? '3' : null }
  static boolean agrUnifiable(String a1, String a2) { return a1 == null || a2 == null || a1 == a2 }

  ParsingState intercept(Map<Construction, Map> constructions, ParsingState state, Function2<Map<Construction, Map>, ParsingState, ParsingState> base) {
    List<Contribution> history
    int similar

    List<Contribution> precedent = state[seq]?.precedent
    if (precedent != null) {
      similar = findSimilar(precedent, constructions)
      if (similar >= 0) {
        history = precedent.reverse()
        similar = history.size() - 1 - similar
      }
    } else {
      history = before.history
      similar = findSimilar(history, constructions)
    }

    if (similar >= 0) {
      Update update = new Update(FLinkedMap.emptyMap)
      def seqs = [:]
      boolean merged = false

      def oldApps = history[similar].apps
      if (conj == 'but' && oldApps[nom]?.head && constructions[nom]?.head) {
        def seqVar = new Variable()
        state = state.assign(seqVar, 'member', oldApps[nom].head).assign(seqVar, 'conj', 'but').assign(seqVar, 'member', constructions[nom].head).
                inhibit(seq)
        if (before[nom]?.noun && !constructions[nom].noun && agree(oldApps[nom].agr, constructions[nom].agr)) {
          state = state.inhibit(nom).apply((nom):[noun:before[nom].noun])
        }
        return base(constructions, state)
      }

      for (cxt in constructions.keySet()) {
        def oldArgs = oldApps[cxt]
        def newUpdate = merge(state, cxt, oldArgs, constructions[cxt], update, seqs, declOrQuestionComp in constructions)
        merged |= newUpdate != null
        update = newUpdate ?: update.addCxt(constructions[cxt], cxt)
      }

      if (merged) {
        def modernHistory = state.history
        if (precedent == null) {
          ParsingState rollback = history[similar].before
          rollback = rollback[numQuantifier]?.prev ?: rollback //todo a more generic way of finding a phrase start
          state = state.clearConstructions().restore(rollback)

          def lastConj = modernHistory.findIndexOf { Contribution it -> it.apps[seq] != null }
          if (lastConj > 0) {
            for (contribution in modernHistory.subList(0, lastConj).reverse()) {
              state = state.apply(contribution.apps)
            }
          }
        } else {
          state = state.inhibit(seq)
        }

        if (similar > 0) {
          List<Contribution> remainingPrecedent = history[0..<similar].reverse() //todo alex infer
          if (poDat in history[similar].apps) { //todo a generic way of finding sequence precedents
            def nounContribution = remainingPrecedent.find { it.apps[dat]?.hasNoun }
            if (nounContribution) {
              update = update.addCxt(seq, precedent:[nounContribution], interceptor:new SeqInterceptor(state, conj))
            }
          }
        }
        return update.apply(state)
      }
    }

    return base(constructions, state)
  }

  Map<String, Object> getArgs(Construction construction, ParsingState state) {
    null
  }


  private Update merge(ParsingState state, Construction cxt, Map oldArgs, Map newArgs, Update update, Map<Pair<Variable, Variable>, Variable> seqs, boolean clauseLevel) {
    if (oldArgs != null) {
      if (conj == 'but') {
        return null
      }

      if (cxt == elaboration && before[cxt]?.head && oldArgs.elaboration && newArgs.elaboration) {
        return doMerge(state, cxt, oldArgs, newArgs, 'elaboration', update, seqs)
      }
      if (cxt == declOrQuestionComp && before[cxt]?.head && oldArgs.comp && newArgs.comp && oldArgs.comp != state[cxt]?.head) {
        return doMerge(state, cxt, oldArgs, newArgs, 'comp', update, seqs)
      }
      if (cxt in [nom] && oldArgs.head && newArgs.head && !newArgs.noun && before[cxt]?.head) {
        return doMerge(state, cxt, before[cxt] ?: oldArgs, newArgs, 'head', update, seqs)
      }

      if (clauseLevel) return null

      if (cxt in [nom, acc, gen, dat] && oldArgs.noun && newArgs.noun) {
        return doMerge(state, cxt, oldArgs, newArgs, 'noun', update, seqs)
      }
      if (cxt in [poDat] && !oldArgs.noun && !newArgs.noun && before[cxt]?.noun && !newArgs.head) {
        def newNoun = new Variable()
        return doMerge(state, cxt, oldArgs + [unmergedNoun: newNoun, noun:before[cxt].noun], newArgs + [noun: newNoun], 'noun', update, seqs)
      }
      if (cxt in [gen] && oldArgs.head && newArgs.head && !newArgs.noun) {
        return doMerge(state, cxt, oldArgs, newArgs, 'head', update, seqs)
      }
      if (cxt == possessive && oldArgs.possessor && newArgs.possessor) {
        return doMerge(state, cxt, oldArgs, newArgs, 'possessor', update, seqs)
      }
      if (cxt == possessive && oldArgs.head && newArgs.head) {
        return doMerge(state, cxt, oldArgs, newArgs, 'head', update, seqs)
      }
    }

    return null
  }


  private Update doMerge(ParsingState state, Construction cxt, Map oldArgs, Map newArgs, String prop, Update update, Map<Pair<Variable, Variable>, Variable> seqs) {
    Variable first = (Variable) oldArgs[prop]
    Variable existingSeq = first.frame(state.chart).f('member') ? first : first.frame(state.chart).usages('member')[0]?.var
    if (existingSeq) {
      def oldConj = existingSeq.frame(state.chart).s('conj')
      if (!oldConj || oldConj == conj) {
        return joinSeq(newArgs, state, cxt, existingSeq, oldArgs, prop, update)
      }
    }

    def second = newArgs[prop]
    Variable multi = seqs[new Pair(first, second)] ?: new Variable("$first&$second")
    seqs[new Pair(first, second)] = multi

    Reference<Construction> seqStart = []
    seqStart.set(Construction.cxt('seqStart') { ParsingState st, Map args ->
      st = st.assign(args.multi, 'member', args.first)
      if (args.conj) {
        st = st.assign(args.multi, 'conj', args.conj)
      }
      if (args.distinguish) {
        st = st.assign(args.second, 'distinguished_in', args.multi)
      }
      st.assign(args.multi, 'member', args.second).satisfied(seqStart.get())
    })

    return update.
            addCxt(seqStart.get(), multi:multi, first:first, second:second, conj:conj, distinguish:newArgs.repeated).
            addCxt(oldArgs + [(prop):multi, xor:ParsingState.mergeXor(oldArgs, newArgs)], cxt)
  }


  static Update joinSeq(Map newArgs, ParsingState state, Construction cxt, Variable seqVar, Map oldArgs, String property, Update update) {
    Reference<Construction> seqNext = []
    seqNext.set(Construction.cxt('seqNext') { ParsingState st, Map args ->
      if (args.conj) {
        st = st.assign(args.multi, 'conj', args.conj)
      }
      if (args.multi != args.next) { //todo long seqs assembling
        st = st.assign(args.multi, 'member', args.next)
      }
      st.satisfied(seqNext.get())
    })
    def newMember = newArgs[property]
    return update.
            addCxt(seqNext.get(), multi:seqVar, next:newMember, conj:state[seq]?.conj).
            addCxt(oldArgs + [(property):seqVar, xor:ParsingState.mergeXor(oldArgs, newArgs)], cxt)

  }

}
