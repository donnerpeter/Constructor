package cons3

/**
 * @author peter
 */

class Chart {
  final List<Situation> situations = []
  final Map<Situation, List<Frame>> frames = [:]
  final Map<Situation, List<Assignment>> _assignments = [:]

  List newSituation() {
    def result = new Situation('#' + (situations.size() + 1), this)
    situations << result
    frames[result] = []
    _assignments[result] = []
    return [this, result]
  }

  List newFrame(Situation situation) {
    def result = new Frame(String.valueOf((char)(frames[situation].size() + (int)'A'.charAt(0))), situation)
    frames[situation] << result
    return [this, result]
  }

  Chart assign(Frame frame, String property, value, boolean rheme) {
    _assignments[frame.situation] << new Assignment(frame, property, value, rheme)
    return this
  }

  String presentable() {
    situations.collect { it.presentable(this) }.join("\n--\n")
  }
}

class Situation extends Frame {
  final Chart chart

  Situation(String id, Chart chart) {
    super(id, null)
    this.chart = chart
  }

  String presentable(Chart chart) {
    chart._assignments[this].collect { def eq ->
      return "${eq.frame == (Situation) this ? 'this' : eq.frame.id}.$eq.property${eq.rheme ? ":=" : "=="}${eq.stringValue()}"
    }.join("\n")
  }

}

class Frame {
  final String id
  final Situation situation

  Frame(String id, Situation situation) {
    this.id = id
    this.situation = situation ?: (Situation) this
  }

  String toString(Chart chart) {
    return "$id: ${chart._assignments[situation].findAll { it.frame == (Frame) this }}"
  }

  Frame f(String attr) {
    def v = allAssignments(attr)[0]?.value
    v instanceof Frame ? (Frame) v : null
  }

  String s(String attr) {
    def v = allAssignments(attr)[0]?.value
    v instanceof String ? (String) v : null
  }

  String getType() { s('type') }

  List<Assignment> allAssignments(String attr) {
    return situation.chart._assignments[situation].findAll { it.frame == (Frame) this && it.property == attr }
  }
}

class Assignment<T> {
  final Frame frame
  final String property
  final T value
  final boolean rheme

  Assignment(Frame frame, String property, def value, boolean rheme) {
    assert frame
    assert property
    this.frame = frame
    this.property = property
    this.value = value
    this.rheme = rheme
  }

  @Override
  String toString() {
    return "$frame.id.$property=${stringValue()}"
  }

  String stringValue() {
    return value instanceof String ? value : ((Frame) value).id
  }

}
