package cons3

/**
 * @author peter
 */

class Chart {
  final List<Situation> situations = []

  Situation newSituation() {
    def result = new Situation('#' + (situations.size() + 1), this)
    situations << result
    return result
  }

  String presentable() {
    situations.collect { it.presentable() }.join("\n--\n")
  }
}

class Situation extends Frame {
  final Chart chart
  final List<Frame> frames = []
  final List<Assignment> _assignments = []
  private final Set<Assignment> constraints = []

  Situation(String id, Chart chart) {
    super(id, null)
    this.chart = chart
  }

  Frame newFrame() {
    def result = new Frame(String.valueOf((char)(frames.size() + (int)'A'.charAt(0))), this)
    frames << result
    return result
  }

  void assign(Frame frame, String property, String value, boolean rheme) {
    _assign(new StringAssignment(frame, property, value), rheme)
  }

  void assign(Frame frame, String property, Frame value, boolean rheme) {
    _assign(new FrameAssignment(frame, property, value), rheme)
  }

  private def _assign(Assignment assignment, boolean rheme) {
    _assignments << assignment
    if (!rheme) {
      constraints << assignment
    }
  }

  String presentable() {
    _assignments.collect { def eq ->
      return "${eq.frame == (Situation) this ? 'this' : eq.frame}.$eq.property${eq in constraints ? "==" : ":="}$eq.value"
    }.join("\n")
  }

}

class Frame {
  private String id
  private Situation situation

  Frame(String id, Situation situation) {
    this.id = id
    this.situation = situation ?: (Situation) this
  }

  String toString() {
    return id
  }

  Frame f(String attr) {
    def asg = allAssignments(attr)[0]
    asg instanceof FrameAssignment ? asg.value : null
  }

  String s(String attr) {
    def asg = allAssignments(attr)[0]
    asg instanceof StringAssignment ? asg.value : null
  }

  String getType() { s('type') }

  List<Assignment> allAssignments(String attr) {
    return situation._assignments.findAll { it.frame == (Frame) this && it.property == attr }
  }
}

class Assignment<T> {
  final Frame frame
  final String property
  final T value

  Assignment(Frame frame, String property, T value) {
    assert frame
    assert property
    this.frame = frame
    this.property = property
    this.value = value
  }

}

class StringAssignment extends Assignment<String> {

  StringAssignment(Frame frame, String property, String value) {
    super(frame, property, value)
  }

}

class FrameAssignment extends Assignment<Frame> {

  FrameAssignment(Frame frame, String property, Frame value) {
    super(frame, property, value)
  }

}